//======================
// Constants and Ranges
//======================

// Alarm State of Pump
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated..AlarmSilenced

// Rate State of Line
const UnsetRate = 0 // Line rate is not set
const SetRate = 1 	// Line rate is set

range RateSetRange = UnsetRate..SetRate

// VTBI State of Line
const UnsetVTBI = 0	// Line VTBI is not set
const SetVTBI = 1	// Line VTBI is set

range VTBISetRange = UnsetVTBI..SetVTBI

// Settings State of Line
const UnsavedSettings = 0 // Settings of a line are not saved
const SavedSettings = 1	  // Settings of a line are saved

range SettingsSavedRange = UnsavedSettings..SavedSettings

// Blocked State of Line
const BlockedState = 0		// Line is blocked
const UnblockedState = 1	// Line is unblocked

range BlockStateRange = BlockedState..UnblockedState

// Ready State of Line, i.e. it is without air, connected, etc.
const NotReadyToOperate = 0	// Line is ready to operate 
const ReadyToOperate = 1	// Line is not ready to operate

range ReadyToOpearateState = NotReadyToOperate..ReadyToOperate

// Charged/uncharged states of the battery
const BatteryCharged = 0
const BatteryUncharged = 1

range BatteryStateT = BatteryCharged .. BatteryUncharged

// On/Off states of the electricity
const ElectricityOn = 0
const ElectricityOff = 1

range ElectricityStateT = ElectricityOn .. ElectricityOff

// On/Off states of the battery indicator
const IndicatorOn = 0
const IndicatorOff = 1

range IndicatorStateT = IndicatorOn .. IndicatorOff

// True/False state if the self test has been launched first time
const FirstLaunchTrue = 0
const FirstLaunchFalse = 1

range FirstLaunchStateT = FirstLaunchTrue .. FirstLaunchFalse

// Number of medication boluses
const VOLUME = 3

// Single dose
const BOLUS = 1 

// Empty medication set volume
const EMPTY_SET = 0

//=====================
// Process Definitions
//=====================
PUMP = TURNED_OFF,

TURNED_OFF = (
	turn_on -> CHECKING_ELECTRICITY[FirstLaunchTrue]
),

TURNED_ON = (
	self_check -> (
		// if checking went well
		success -> SETUP 
		|
		// else
	 	fail -> fix_problems -> TURNED_ON
		|
		power_outage -> CHECKING_ELECTRICITY[FirstLaunchTrue]
		|
		turn_off -> TURNED_OFF
	)
	|
	power_outage -> CHECKING_ELECTRICITY[FirstLaunchTrue]
	|
	turn_off -> TURNED_OFF
),

CHECKING_ELECTRICITY[state: FirstLaunchStateT] = (
	when(state == FirstLaunchTrue)
		check_electricity -> TURNED_ON
		|
		check_electricity -> POWERED_BY_ELECTRICITY[ElectricityOff][FirstLaunchTrue]
	|
	when(state == FirstLaunchFalse)
		check_electricity -> SETUP
		|
		check_electricity -> POWERED_BY_ELECTRICITY[ElectricityOff][FirstLaunchFalse]
	|
	turn_off -> TURNED_OFF
),

POWERED_BY_BATTERY[state:BatteryStateT][launch:FirstLaunchStateT] = 
(
	when (state == BatteryCharged)
		turn_on_battery -> TURNED_ON
	|
	when (state == BatteryUncharged)
		change_battery -> POWERED_BY_BATTERY[BatteryCharged][launch]
	|
	when (state == BatteryUncharged)
		go_to_check_electricity -> CHECKING_ELECTRICITY[launch]
	|
	turn_off -> TURNED_OFF
),

POWERED_BY_ELECTRICITY[state:ElectricityStateT][launch:FirstLaunchStateT] = (
	when (state == ElectricityOn)
		turn_on -> TURNED_ON
	|
	when (state == ElectricityOff)
		switch_on_battery -> POWERED_BY_BATTERY[BatteryCharged][launch]
	| 
	when (state == ElectricityOff)	
		switch_on_battery -> POWERED_BY_BATTERY[BatteryUncharged][launch]
	|
	turn_off -> TURNED_OFF
),

SETUP = (
	// we confirm the settings of all lines
	confirm_saved_lines -> (
		// start the infusion
		start_infusion -> INFUSION[VOLUME] 
		|
		// if error - go back to self-check
		error -> TURNED_ON
		|
		power_outage -> CHECKING_ELECTRICITY[FirstLaunchFalse]
	)
	| 
	cancel_all_settings -> SETUP
	|
 	// if error - go back to self-check
	error -> TURNED_ON
	|
	power_outage -> CHECKING_ELECTRICITY[FirstLaunchFalse]
	|
	turn_off -> TURNED_OFF
),

INFUSION[amount:0..VOLUME] = (
	// when amount is greater than volume of empty set
	// we infuse single bolus
	when(amount>EMPTY_SET) infuse -> INFUSION[amount-BOLUS]
	|
	stop_infusion -> (
		stop_pump -> turn_off -> TURNED_OFF 
		| 
		resume_infusion -> INFUSION[amount]
		|
		// when empty - refill the pump
		when(amount==EMPTY_SET) refill_pump -> SETUP
		|
		error -> TURNED_ON
		|
		power_outage -> CHECKING_ELECTRICITY[FirstLaunchFalse]
	)
	|
	// during infusion a line can be blocked
	block_line -> (
		// mitigation is to unblock it
		// and continue with infusion
		unblock_line -> INFUSION[amount]
		|
		error -> TURNED_ON
		|
		power_outage -> CHECKING_ELECTRICITY[FirstLaunchFalse]
	)
	|
	error -> TURNED_ON
	| 
	power_outage -> CHECKING_ELECTRICITY[FirstLaunchFalse]
).


// initially, line is not ready to operate (i.e. not connected, etc.)
// it is unblocked,
// rate and vtbi are not set,
// settings are not saved
INITIAL_LINE = LINE[NotReadyToOperate][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings],

LINE[ready:ReadyToOpearateState][block:BlockStateRange][rate:RateSetRange][vtbi:VTBISetRange][settings:SettingsSavedRange] = (
	when (ready == NotReadyToOperate)
		ready -> LINE[ReadyToOperate][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == UnsetRate && settings == UnsavedSettings)
		set_rate -> LINE[ready][block][SetRate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && vtbi == UnsetVTBI && settings == UnsavedSettings)
		set_vtbi -> LINE[ready][block][rate][SetVTBI][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		confirm_settings -> LINE[ready][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == UnsavedSettings)
		save_settings -> LINE[ready][block][rate][vtbi][SavedSettings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && settings == SavedSettings)
		discard_settings -> LINE[ready][block][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		unblock_line -> LINE[ready][UnblockedState][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		sound_alarm -> LINE[ready][BlockedState][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate & block == UnblockedState)
		block_line -> LINE[ready][BlockedState][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		stop -> INITIAL_LINE
	|
	error -> INITIAL_LINE
).

// Assume that N is a number of channels to setup!
||PUMP_CHANNEL(N=2) = (l[i:1..N]:INITIAL_LINE || PUMP)
			/{success/l[1..N].ready, //if the self-check is fine - lines are ready to operate
				//as we set all the lines and saved the settings
				confirm_saved_lines/l[1..N].confirm_settings, // we can confirm them
				cancel_all_settings/l[1..N].discard_settings, // or cancel
				stop_pump/l[1..N].stop, //if we turn off the pump - the lines are stopped
				// if the error occured - the line is not ready to operate
				error/l[1..N].error, // and settings are discarded
				l[1..N].block_line/block_line, //blocking any line will result in either error
				l[1..N].unblock_line/unblock_line,
				power_outage/l[1..N].error}. //or line unblocking
