//======================
// Constants and Ranges
//======================

// Alarm State of Pump
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated..AlarmSilenced

// Rate State of Line
const UnsetRate = 0 // Line rate is not set
const SetRate = 1 	// Line rate is set

range RateSetRange = UnsetRate..SetRate

// VTBI State of Line
const UnsetVTBI = 0	// Line VTBI is not set
const SetVTBI = 1	// Line VTBI is set

range VTBISetRange = UnsetVTBI..SetVTBI

// Settings State of Line
const UnsavedSettings = 0 // Settings of a line are not saved
const SavedSettings = 1	  // Settings of a line are saved

range SettingsSavedRange = UnsavedSettings..SavedSettings

// Blocked State of Line
const BlockedState = 0		// Line is blocked
const UnblockedState = 1	// Line is unblocked

range BlockStateRange = BlockedState..UnblockedState

// Ready State of Line, i.e. it is without air, connected, etc.
const NotReadyToOperate = 0	// Line is ready to operate 
const ReadyToOperate = 1	// Line is not ready to operate

range ReadyToOpearateState = NotReadyToOperate..ReadyToOperate

// Number of medication boluses
const VOLUME = 3

// Single dose
const BOLUS = 1 

// Empty medication set volume
const EMPTY_SET = 0

//=====================
// Process Definitions
//=====================
PUMP = TURNED_OFF,

TURNED_OFF = (turn_on -> TURNED_ON),

TURNED_ON = (
	self_check -> (
		// if checking went well
		success -> SETUP 
		|
		// else
	 	fail -> fix_problems -> TURNED_ON
	)
),

SETUP = (
	// we confirm the settings of all lines
	confirm_saved_lines -> (
		// start the infusion
		start_infusion -> INFUSION[VOLUME] 
		|
		// if error - go back to self-check
		error -> TURNED_ON
	)
	| 
	cancel_all_settings -> SETUP
	|
 	// if error - go back to self-check
	error -> TURNED_ON
),

INFUSION[amount:0..VOLUME] = (
	// when amount is greater than volume of empty set
	// we infuse single bolus
	when(amount>EMPTY_SET) infuse -> INFUSION[amount-BOLUS]
	|
	stop_infusion -> (
		turn_off -> TURNED_OFF 
		| 
		resume_infusion -> INFUSION[amount]
		|
		// when empty - refill the pump
		when(amount==EMPTY_SET) refill_pump -> SETUP
		|
		error -> TURNED_ON
	)
	|
	// during infusion a line can be blocked
	block_line -> (
		// mitigation is to unblock it
		// and continue with infusion
		unblock_line -> INFUSION[amount]
		|
		error -> TURNED_ON
	)
	|
	error -> TURNED_ON
).

INITIAL_LINE = LINE[NotReadyToOperate][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings],

LINE[ready:ReadyToOpearateState][block:BlockStateRange][rate:RateSetRange][vtbi:VTBISetRange][settings:SettingsSavedRange] = (
	when (ready == ReadyToOperate && block == UnblockedState && rate == UnsetRate && settings == UnsavedSettings)
		set_rate -> LINE[ready][block][SetRate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && vtbi == UnsetVTBI && settings == UnsavedSettings)
		set_vtbi -> LINE[ready][block][rate][SetVTBI][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		confirm_settings -> LINE[ready][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == UnsavedSettings)
		save_settings -> LINE[ready][block][rate][vtbi][SavedSettings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && settings == SavedSettings)
		discard_settings -> LINE[ready][block][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		unblock_line -> LINE[ready][UnblockedState][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		sound_alarm -> LINE[ready][BlockedState][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate & block == UnblockedState)
		block_line -> LINE[ready][BlockedState][rate][vtbi][settings]
	|
	when (ready == NotReadyToOperate)
		ready -> LINE[ReadyToOperate][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		stop -> INITIAL_LINE
	|
	error -> INITIAL_LINE
).

// Assume that N is a number of channels to setup!
||PUMP_CHANNEL(N=2) = (l[i:1..N]:INITIAL_LINE || PUMP)
			/{success/l[1..N].ready,
				confirm_saved_lines/l[1..N].confirm_settings,
				cancel_all_settings/l[1..N].discard_settings,
				turn_off/l[1..N].stop,
				error/l[1..N].error,
				l[1..N].block_line/block_line,
				l[1..N].unblock_line/unblock_line}.
