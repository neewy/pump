//======================
// Constants and Ranges
//======================
// Alarm State of Pump
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated..AlarmSilenced

// Rate State of Line
const UnsetRate = 0
const SetRate = 1

range RateSetRange = UnsetRate..SetRate

// VTBI State of Line
const UnsetVTBI = 0
const SetVTBI = 1

range VTBISetRange = UnsetVTBI..SetVTBI

// Settings State of Line
const UnsavedSettings = 0
const SavedSettings = 1

range SettingsSavedRange = UnsavedSettings..SavedSettings

// Blocked State of Line
const BlockedState = 0
const UnblockedState = 1

range BlockStateRange = BlockedState..UnblockedState

// Ready State of Line
const NotReadyToOperate = 0
const ReadyToOperate = 1

range ReadyToOpearateState = NotReadyToOperate..ReadyToOperate

// Number of Infusion
const I = 3

//=====================
// Process Definitions
//=====================
PUMP = TURNED_OFF,

TURNED_OFF = (turn_on -> TURNED_ON),

TURNED_ON = (
	self_check -> (success -> SETUP | fail -> fix_problems -> TURNED_ON)
),

SETUP = (
	confirm_saved_lines -> (start_infusion -> INFUSION[I] | error -> TURNED_ON)
	| 
	cancel_all_settings -> SETUP
	| 
	error -> TURNED_ON
),

INFUSION[n:0..I] = (
	when(n>0) infuse -> INFUSION[n-1]
	|	
	stop_infusion -> (
		turn_off -> TURNED_OFF 
		| 
		resume_infusion -> INFUSION[n]
		|
		when(n==0) refill_pump -> SETUP
		|
		error -> TURNED_ON
	)
	|
	block_line -> (
		unblock_line -> INFUSION[n]
		|
		error -> TURNED_ON
	)
	|
	error -> TURNED_ON
),

BLOCKED[alarm:AlarmStateT] =
(
	turn_off -> TURNED_OFF
	|
    when (alarm == AlarmActivated)
        sound_alarm -> BLOCKED[alarm]
    |
    when (alarm == AlarmActivated)
        silence_alarm -> BLOCKED[AlarmSilenced]
    |
    unblock -> TURNED_ON
).

INITIAL_LINE = LINE[NotReadyToOperate][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings],

LINE[ready:ReadyToOpearateState][block:BlockStateRange][rate:RateSetRange][vtbi:VTBISetRange][settings:SettingsSavedRange] = (
	when (ready == ReadyToOperate && block == UnblockedState && rate == UnsetRate && settings == UnsavedSettings)
		set_rate -> LINE[ready][block][SetRate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && vtbi == UnsetVTBI && settings == UnsavedSettings)
		set_vtbi -> LINE[ready][block][rate][SetVTBI][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		confirm_settings -> LINE[ready][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == UnsavedSettings)
		save_settings -> LINE[ready][block][rate][vtbi][SavedSettings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && settings == SavedSettings)
		discard_settings -> LINE[ready][block][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		unblock_line -> LINE[ready][UnblockedState][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		sound_alarm -> LINE[ready][BlockedState][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate & block == UnblockedState)
		block_line -> LINE[ready][BlockedState][rate][vtbi][settings]
	|
	when (ready == NotReadyToOperate)
		ready -> LINE[ReadyToOperate][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		stop -> LINE[NotReadyToOperate][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	error -> INITIAL_LINE
).

||PUMP_CHANNEL(N=2) = (l[i:1..N]:INITIAL_LINE || PUMP)
			/{success/l[1..N].ready,
				confirm_saved_lines/l[1..N].confirm_settings,
				cancel_all_settings/l[1..N].discard_settings,
				turn_off/l[1..N].stop,
				error/l[1..N].error,
				l[1..N].block_line/block_line,
				l[1..N].unblock_line/unblock_line}.
