//======================
// Constants and Ranges
//======================
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated .. AlarmSilenced

// Line State of Channel
const WithoutLine = 0
const WithLine = 1

range ReadyToOperateRange = WithoutLine..WithLine

// Rate State of Channel
const UnsetRate = 0
const SetRate = 1

range RateSetRange = UnsetRate..SetRate


// VTBI State of Channel
const UnsetVTBI = 0
const SetVTBI = 1

range VTBISetRange = UnsetVTBI..SetVTBI

// Settings State of Channel
const UnsavedSettings = 0
const SavedSettings = 1

range SettingsSavedRange = UnsavedSettings..SavedSettings

// Blocked State of Line
const BlockedState = 0
const UnblockedState = 1

range BlockStateRange = BlockedState..UnblockedState

const NotReadyToOperate = 0
const ReadyToOperate = 1

range ReadyToOpearateState = NotReadyToOperate..ReadyToOperate



//=====================
// Process Definitions
//=====================
PUMP = TURNED_OFF,

TURNED_OFF = (turn_on -> TURNED_ON),

TURNED_ON = (self_check -> SETUP
			//|
			//error ->
			//block_line -> BLOCKED[AlarmActivated]
			),

SETUP = (confirm_saved_lines -> start_infusion -> START | cancel_all_settings -> SETUP),

START = (infuse ->  INFUSION),

INFUSION = (stop_infusion -> TURNED_OFF),

BLOCKED[alarm:AlarmStateT] =
(
	turn_off -> TURNED_OFF
	|
    when (alarm == AlarmActivated)
        sound_alarm -> BLOCKED[alarm]
    |
    when (alarm == AlarmActivated)
        silence_alarm -> BLOCKED[AlarmSilenced]
    |
    unblock -> TURNED_ON
).

INITIAL_LINE = LINE[NotReadyToOperate][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings],

LINE[ready:ReadyToOpearateState][block:BlockStateRange][rate:RateSetRange][vtbi:VTBISetRange][settings:SettingsSavedRange] = (
	when (ready == ReadyToOperate && block == UnblockedState && rate == UnsetRate && settings == UnsavedSettings)
		set_rate -> LINE[ready][block][SetRate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && vtbi == UnsetVTBI && settings == UnsavedSettings)
		set_vtbi -> LINE[ready][block][rate][SetVTBI][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		confirm_settings -> LINE[ready][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == UnsavedSettings)
		save_settings -> LINE[ready][block][rate][vtbi][SavedSettings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && settings == SavedSettings)
		discard_settings -> LINE[ready][block][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate && block == BlockedState)
		unblock_line -> LINE[ready][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings]
	|
	when (ready == ReadyToOperate & block == UnblockedState)
		block_line -> LINE[ready][BlockedState][rate][vtbi][settings]
	|
	//when (ready == ReadyToOperate)
		//if settings were set?
		//if infusion began?
		//disconnect_line -> LINE[NotReadyToOperate][block][rate][vtbi][settings]
	//|
	when (ready == NotReadyToOperate)
		ready -> LINE[ReadyToOperate][block][rate][vtbi][settings]
	|
	when (ready == ReadyToOperate && block == UnblockedState && rate == SetRate && vtbi == SetVTBI && settings == SavedSettings)
		stop -> LINE[NotReadyToOperate][UnblockedState][UnsetRate][UnsetVTBI][UnsavedSettings]
).


||PUMP_CHANNEL(N=2) = (l[i:1..N]:INITIAL_LINE || PUMP)
			/{self_check/l[1..N].ready,
				confirm_saved_lines/l[1..N].confirm_settings,
				cancel_all_settings/l[1..N].discard_settings,
				stop_infusion/l[1..N].stop}.
